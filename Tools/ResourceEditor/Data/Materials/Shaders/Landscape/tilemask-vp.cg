#include "common.cgh"

#if USE_INSTANCING

	VPROG_IN_BEGIN
		VPROG_IN_STREAM_VERTEX
			VPROG_IN_TEXCOORD0(4) //position + gluDirection
			VPROG_IN_TEXCOORD1(4) //edge mask
			VPROG_IN_TEXCOORD2(2) //edgeVertexIndex + edgeMaskNull
		VPROG_IN_STREAM_INSTANCE
			VPROG_IN_TEXCOORD3(3) //patch position + scale
			VPROG_IN_TEXCOORD4(4) //near patch lodOffset
		#if LOD_MORPHING
			VPROG_IN_TEXCOORD5(4) //near patch morph
			VPROG_IN_TEXCOORD6(3) //patch lod + morph + pixelMappingOffset
		#endif
	VPROG_IN_END
	
#else
	
	VPROG_IN_BEGIN
		VPROG_IN_POSITION
		VPROG_IN_TEXCOORD
	VPROG_IN_END
	
#endif

VPROG_OUT_BEGIN
    
    VPROG_OUT_POSITION
    VPROG_OUT_TEXCOORD0(texCoord,2)
    #if TILEMASK
        VPROG_OUT_TEXCOORD1(texCoordTiled,2)
    #endif

    #if VERTEX_FOG
        VPROG_OUT_TEXCOORD5(varFog,4)
    #endif

VPROG_OUT_END

#if USE_INSTANCING
	DECL_VP_SAMPLER2D(heightmap)
#endif

property float4x4 worldViewProjMatrix          : dynamic,a : ;

#if USE_INSTANCING
	property float3 boundingBoxSize 		   : dynamic,a : ;
#endif

#if TILEMASK
    property float2   textureTiling 		   : static,instance : def_value=50,50;
#endif

#if VERTEX_FOG
property float4x4 worldMatrix                  : dynamic,a : ;
property float4x4 worldViewMatrix              : dynamic,a : ;
property float3   cameraPosition               : dynamic,a : ;
#endif
#if VERTEX_FOG && FOG_ATMOSPHERE
property float4   lightPosition0               : dynamic,a : ;
#endif

#include "vp-fog-props.cgh"

VPROG_BEGIN

#if USE_INSTANCING

    float2 in_pos = VP_IN_TEXCOORD0.xy;
    float2 gluDir = VP_IN_TEXCOORD0.zw;
    float4 edgeMask = VP_IN_TEXCOORD1;
	float edgeVertexIndex = VP_IN_TEXCOORD2.x;
	
    float3 patchOffsetScale = VP_IN_TEXCOORD3.xyz;
	float4 nearPatchLodOffset = VP_IN_TEXCOORD4;
	
	float lodOffset = dot(edgeMask, nearPatchLodOffset);	
    float gluAmount = pow(2.0, lodOffset);
	in_pos += gluDir * fmod(edgeVertexIndex, gluAmount);
	
    float2 relativePosition = patchOffsetScale.xy + in_pos.xy * patchOffsetScale.z; //[0, 1]
	
#if LOD_MORPHING
	
	float edgeMaskNull = VP_IN_TEXCOORD2.y;
	float4 nearPatchMorph = VP_IN_TEXCOORD5;
	
	float baseLod = VP_IN_TEXCOORD6.x;
	float patchMorph = VP_IN_TEXCOORD6.y;
	float basePixelOffset = VP_IN_TEXCOORD6.z;
	
	
	float2 zeroLod = STEP(1.0, relativePosition);
	float zeroLodMul = 1.0 - min(1.0, zeroLod.x + zeroLod.y);

	float sampleLod = (baseLod + lodOffset) * zeroLodMul;
	float samplePixelOffset = basePixelOffset * gluAmount * zeroLodMul;
	float4 heightmapSample = VP_TEXTURE2D(heightmap, relativePosition + samplePixelOffset, sampleLod);
	
	float morphAmount = dot(edgeMask, nearPatchMorph) + patchMorph * edgeMaskNull;
	float2 hmSampleMorphed = lerp(heightmapSample.zw, heightmapSample.xy, morphAmount);
	float height = dot(hmSampleMorphed, float2(0.0038910506, 0.99610895));
	
	//// height calculation equal to:
	//float h0 = dot(heightmapSample.xy, float2(0.0038910506, 0.99610895)); // 'accurate' heightmap value
	//float h1 = dot(heightmapSample.zw, float2(0.0038910506, 0.99610895)); // 'averaged' heightmap value
	//float height = lerp(h1, h0, morphAmount);
	
#else
	
	float4 heightmapSample = VP_TEXTURE2D(heightmap, relativePosition, 0.0);
	float height = dot(heightmapSample, float4(0.00022888532845, 0.00366216525521, 0.05859464408331, 0.93751430533303));
	
#endif

    float3 vx_position = float3( relativePosition - 0.5, height ) * boundingBoxSize;
	
    VP_OUT_POSITION = mul( float4(vx_position.x, vx_position.y, vx_position.z, 1.0), worldViewProjMatrix );
    VP_OUT(texCoord) = float2(relativePosition.x, 1.0 - relativePosition.y);
	 
#else
	
	float3 vx_position = VP_IN_POSITION.xyz;
    
    VP_OUT_POSITION = mul( float4(vx_position.x, vx_position.y, vx_position.z, 1.0), worldViewProjMatrix );
	VP_OUT(texCoord) = VP_IN_TEXCOORD;
	
#endif
	
#if TILEMASK
    VP_OUT(texCoordTiled) = VP_OUT(texCoord) * textureTiling.xy;
#endif
	
#if VERTEX_FOG
	
	float3 view_position = mul( float4(vx_position.xyz,1.0), worldViewMatrix ).xyz;
	#define FOG_view_position view_position
	
#if FOG_ATMOSPHERE
	float3 tolight_dir = lightPosition0.xyz - view_position * lightPosition0.w;
    #define FOG_to_light_dir tolight_dir
#endif
	
#if FOG_HALFSPACE || FOG_ATMOSPHERE_MAP
	float3 world_position = mul( float4(vx_position.xyz,1.0), worldMatrix ).xyz;
	#define FOG_world_position world_position
#endif

    #define FOG_eye_position cameraPosition

	#include "vp-fog-math.cgh" // in{ float3 FOG_view_position, float3 FOG_eye_position, float3 FOG_to_light_dir, float3 FOG_world_position }; out{ float4 FOG_result };
	
	VP_OUT(varFog) = FOG_result;
	
#endif
    
VPROG_END
